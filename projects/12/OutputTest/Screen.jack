// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static Array twoToThe;
    static boolean color;
    static int SCREEN;
    
    /** Initializes the Screen. */
    function void init() {
        let twoToThe = Array.new(15);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 16384 + 16384;

        let SCREEN = 16384;
        let color = true;
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i, screenEnd;
        let screenEnd = 24576;
        let i = SCREEN;

        while (i < screenEnd) {
            do Memory.poke(i, 0);
            let i = i + 1;
        }

        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        /*
            address = 32 * y + x /16
            value = Memory.peek[16384 + address]
            set the (x % 16)th bit of value to the current color using OR
            do Memory.poke(address, value)
        */

        var int address, value;
        let address = SCREEN + (32 * y) + (x / 16);
        let value = Memory.peek(address);
        if (color) {
            let value = value | twoToThe[x & 15]; // x % 16 == x & 15 for positive x
        } else {
            let value = value & ~twoToThe[x & 15];
        }
        do Memory.poke(address, value);

        return;
    }

     /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {

        // Bresenhamâ€™s Algorithm 
        var int x, y, dx, dy, dx1, dy1, px, py, xe, ye, i;

        let dx = x2 - x1;
        let dy = y2 - y1;

        let dx1 = Math.abs(dx);
        let dy1 = Math.abs(dy);

        let px = 2 * dy1 - dx1;
        let py = 2 * dx1 - dy1;

        if (~(dy1 > dx1)) {
            if (~(dx < 0)) {
                let x = x1;
                let y = y1;
                let xe = x2;
            } else {
                let x = x2;
                let y = y2;
                let xe = x1;
            }
            do Screen.drawPixel(x, y);

            while (x < xe) {
                let x = x + 1;
                if (px < 0) {
                    let px = px + (2 * dy1);
                } else {
                    if (((dx < 0) & (dy < 0)) | ((dx > 0) & (dy > 0))) {
                        let y = y + 1;
                    } else {
                        let y = y - 1;
                    }
                    let px = px + (2 * (dy1 - dx1));
                }
                do Screen.drawPixel(x, y);
            }
        } else {
            if (~(dy < 0)) {
                let x = x1;
                let y = y1;
                let ye = y2;
            } else {
                let x = x2;
                let y = y2;
                let ye = y1;
            }
            do Screen.drawPixel(x, y);

            while (y < ye) {
                let y = y + 1;

                if (~(py > 0)) {
                    let py = py + (2 * dx1);
                } else {
                    if (((dx < 0) & (dy < 0)) | ((dx > 0) & (dy > 0))) {
                        let x = x + 1;
                    } else {
                        let x = x - 1;
                    }
                    let py = py + (2 * (dx1 - dy1));
                }
                do Screen.drawPixel(x, y);
            }
        }

        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int y;
        let y = y1;

        while (~(y > y2)) {
            do Screen.drawLine(x1, y, x2, y);
            let y = y + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy, h;

        if (r > 181) {
            return;
        }

        let dy = -r;

        while (~(dy > r)) {
            let h = Math.sqrt((r * r) - (dy * dy));
            do Screen.drawLine(x - h, y + dy, x + h, y + dy);
            let dy = dy + 1;
        }
        return;
    }
}
